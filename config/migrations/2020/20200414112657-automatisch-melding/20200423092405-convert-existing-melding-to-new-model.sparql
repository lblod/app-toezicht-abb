PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX melding: <http://lblod.data.gift/vocabularies/automatische-melding/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX schema: <http://schema.org/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?review adms:status ?reviewStatus ;
      schema:reviewBody ?reviewBody .
  }
} INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?review adms:status ?mappedStatus ;
      schema:reviewBody ?meldingComment .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
    ?review  <http://schema.org/itemReviewed> ?submission ;
      adms:status ?reviewStatus .
    OPTIONAL { ?review schema:reviewBody ?reviewBody . }
    ?submission dct:source ?inzending .
    ?melding dct:subject ?inzending ;
      adms:status ?meldingStatus .
    OPTIONAL { ?melding dct:description ?meldingComment . }

    BIND(IF(?meldingStatus = <http://data.lblod.info/melding-statuses/afgehandeld>, <http://lblod.data.gift/concepts/5b36ed48-491c-4177-ae31-31338dc46de1>, <http://lblod.data.gift/concepts/d72999bf-2ef4-4332-bd6e-49c8a9e2498e>) as ?mappedStatus)
  }
}


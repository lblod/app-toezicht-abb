#################################################################
# Links all forms from 2019 to the correct
# toezicht:InzendingVoorToezichtFormVersion
#################################################################
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX dct: <http://purl.org/dc/terms/>

# create a new form-version for new form node
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
   <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f> mu:uuid "b925a87f-90dd-4f5a-8f14-c078ef2c3e3f".
   <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f> dct:description "In voege vanaf 02/01/2019".
   <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f> a toezicht:InzendingVoorToezichtFormVersion.
   <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f> toezicht:inzendingVoorToezichtFormVersionStart "2019-01-02"^^xsd:date.
   <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f> toezicht:inzendingVoorToezichtFormVersionFormNode <http://data.lblod.info/form-nodes/3aa9e6897f9048d67af54837127db5bafb58aaa689bab1842510f0b17e6b1c05>.
  }
}

;

# link new code lists with version
INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
  ?s toezicht:formVersionScope <http://data.lblod.info/inzending-voor-toezicht-form-version/b925a87f-90dd-4f5a-8f14-c078ef2c3e3f>.
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/application> {
    ?s a  toezicht:DecisionType.
  }
}

;

# clean application graph
DELETE {
  GRAPH <http://mu.semte.ch/application> {
  ?s ?p ?o.
 }
}

INSERT {
  GRAPH <http://mu.semte.ch/graphs/organizations/141d9d6b-54af-4d17-b313-8d1c30bc3f5b> {
  ?s ?p ?o.
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/application> {

    ?s a  ?type.
    FILTER (?type IN (ext:FormInput, ext:FormNode, ext:DynamicSubform, ext:FormSolution,
                      toezicht:TaxRate, toezicht:DecisionType, toezicht:InzendingType, toezicht:RegulationType,
                      toezicht:DocumentAuthenticityType, toezicht:AccountAcceptanceStatus, toezicht:DeliveryReportType,
                      toezicht:FiscalPeriod, toezicht:Nomenclature, toezicht:TaxType, toezicht:InzendingVoorToezicht, ext:InputState ) ).
    ?s ?p ?o.
 }
}

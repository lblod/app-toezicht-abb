PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# Nieuwe bestuursorganen (en in tijd) algemene vergadering en raad van bestuur voor AGB. APB

INSERT {
  GRAPH ?g {

    ?bestuursorgaanAlgVerg a besluit:Bestuursorgaan .
    ?bestuursorgaanAlgVerg besluit:bestuurt ?bestuurseenheid.
    ?bestuursorgaanAlgVerg mu:uuid ?uuidAlgVerg .
    ?bestuursorgaanAlgVerg skos:prefLabel ?algNaam .
    ?bestuursorgaanAlgVerg besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/b52094ff-21a2-4da8-8dbe-f513365d1528> .

    ?bestuursorgaanAlgVergInTijd a besluit:Bestuursorgaan .
    ?bestuursorgaanAlgVergInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaanAlgVerg.
    ?bestuursorgaanAlgVergInTijd mandaat:bindingStart "2019-01-01"^^<http://www.w3.org/2001/XMLSchema#date> .
    ?bestuursorgaanAlgVergInTijd mu:uuid ?uuidAlgVergInTijd .


    ?bestuursorgaanRVB a besluit:Bestuursorgaan .
    ?bestuursorgaanRVB besluit:bestuurt ?bestuurseenheid .
    ?bestuursorgaanRVB mu:uuid ?uuidRVB .
    ?bestuursorgaanRVB skos:prefLabel ?RVBNaam.
    ?bestuursorgaanRVB besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/013cc838-173a-4657-b1ae-b00c048df943> .

    ?bestuursorgaanRVBInTijd a besluit:Bestuursorgaan .
    ?bestuursorgaanRVBInTijd mandaat:isTijdspecialisatieVan ?bestuursorgaanRVB.
    ?bestuursorgaanRVBInTijd mandaat:bindingStart "2019-01-01"^^<http://www.w3.org/2001/XMLSchema#date> .
    ?bestuursorgaanRVBInTijd mu:uuid ?uuidRVBInTijd .

  }
} WHERE {
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid;
       skos:prefLabel ?naam;
       besluit:classificatie ?classificatie .

    FILTER (?classificatie IN (<http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/36a82ba0-7ff1-4697-a9dd-2e94df73b721>, <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/80310756-ce0a-4a1b-9b8e-7c01b6cc7a2d>)) .

    BIND(SHA256(CONCAT("cd9ce661-b071-46fb-8493-0be0c3ac14b7", ":", STR(?bestuurseenheid))) AS ?uuidAlgVerg) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidAlgVerg))) AS ?bestuursorgaanAlgVerg) .
    BIND(CONCAT("Algemene vergadering ", ?naam) AS ?algNaam).

    BIND(SHA256(CONCAT("332eec49-3c5f-4c03-9763-f3a877234d15", ":", STR(?bestuurseenheid))) AS ?uuidAlgVergInTijd) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidAlgVergInTijd))) AS ?bestuursorgaanAlgVergInTijd) .

    BIND(SHA256(CONCAT("8f9872eb-cbc0-4e62-9029-055b4236fce9", ":", STR(?bestuurseenheid))) AS ?uuidRVB) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidRVB))) AS ?bestuursorgaanRVB) .
    BIND(CONCAT("Raad van bestuur ", ?naam) AS ?RVBNaam).

    BIND(SHA256(CONCAT("2844b4c4-37cf-4189-a251-5bd5760f7229", ":", STR(?bestuurseenheid))) AS ?uuidRVBInTijd) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/bestuursorganen/", STR(?uuidRVBInTijd))) AS ?bestuursorgaanRVBInTijd) .

  }
}

;